onintr -
if (! $?prompt) goto end

set noglob
if ( ! $?hosts ) set hosts
if ( -r "$HOME/.ssh/known_hosts" ) then
    set f=`cat "$HOME/.ssh/known_hosts" | cut -f 1 -d \ ` >& /dev/null
    set f=`cat "$HOME/.ssh/known_hosts" | cut -f 1 -d \ | sed -e 's/,/ /g'` >& /dev/null
    set hosts=($hosts $f)
endif
unset f
if ( ! $?hosts ) then
    set hosts=(hyperion.ee.cornell.edu phaeton.ee.cornell.edu \
    	   guillemin.ee.cornell.edu vangogh.cs.berkeley.edu \
    	   ftp.uu.net prep.ai.mit.edu export.lcs.mit.edu \
    	   labrea.stanford.edu sumex-aim.stanford.edu \
    	   tut.cis.ohio-state.edu)
endif

complete ssh	p/1/\$hosts/ c/-/"(l n)"/   n/-l/u/ N/-l/c/ n/-/c/ p/2/c/ p/*/f/

# not as good as bash...
complete scp "c,*:/,F:/," "c,*:,F:$HOME," 'c/*@/$hosts/:/'

complete cd 'p/1/d/'
complete pushd 'p/1/d/'
complete popd 'p/1/d/'
complete pu 'p/1/d/'
complete po 'p/1/d/'
# For man and sudo, complete commands like they're 1st
# command
complete sudo 'p/1/c/'
complete man 'p/1/c/'
complete port 'p/1/( \
  outdated \
  install \
  uninstall \
  upgrade \
  selfupdate \
  installed \
  search \
  list \
  contents \
  deps \
  variants \
  version \
  info )/'
complete which 'p/1/c/'
#? complete which	n/*/c/
complete wh 'p/1/c/'
complete w3m 'n/*/f:*.{html,htm}/'
complete gunzip 'n/*/f:*.{gz,tgz}/'
complete tar 'n/*/f:*.{tar,tgz,tbz}/'

complete ping	p/1/\$hosts/

complete bzcat	c/--/"(help test quiet verbose license version)"/ \
    		c/-/"(h t L V -)"/ n/*/f:*.{bz2,tbz}/
complete bunzip2	c/--/"(help keep force test stdout quiet verbose \
                    license version)"/ c/-/"(h k f t c q v L V -)"/ \
    		n/*/f:*.{bz2,tbz}/
complete bzip2	c/--/"(help decompress compress keep force test \
    		stdout quiet verbose license version small)"/ \
    		c/-/"(h d z k f t c q v L V s 1 2 3 4 5 6 7 8 9 -)"/ \
    		n/{-d,--decompress}/f:*.{bz2,tbz}/ \
    		N/{-d,--decompress}/f:*.{bz2,tbz}/ n/*/f:^*.{bz2,tbz}/

unset noglob
unset _complete
unset traditional_complete

end:
	onintr
